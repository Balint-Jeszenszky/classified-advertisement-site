import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
	id 'org.springframework.boot' version '3.1.4'
	id 'io.spring.dependency-management' version '1.1.3'
	id 'org.jetbrains.kotlin.jvm' version '1.8.22'
	id 'org.jetbrains.kotlin.plugin.spring' version '1.8.22'
	id 'org.jetbrains.kotlin.plugin.jpa' version '1.8.22'
	id 'org.openapi.generator' version '6.5.0'
	id "org.jetbrains.kotlin.kapt" version "1.8.20-RC2"
}

group = 'hu.bme.aut.classifiedadvertisementsite'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-amqp'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
	implementation 'org.jetbrains.kotlin:kotlin-reflect'
	implementation 'org.liquibase:liquibase-core'
	implementation 'org.springframework.boot:spring-boot-starter-websocket'
	implementation "javax.servlet:servlet-api:2.5"
	implementation "javax.validation:validation-api:2.0.1.Final"
	implementation "io.swagger.parser.v3:swagger-parser:2.1.16"
	implementation 'org.mapstruct:mapstruct:1.5.5.Final'
	implementation 'org.springframework.boot:spring-boot-starter-amqp'
	implementation 'javax.annotation:javax.annotation-api:1.3.2'
	implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
	implementation 'net.javacrumbs.shedlock:shedlock-spring:5.9.1'
	implementation 'net.javacrumbs.shedlock:shedlock-provider-jdbc-template:5.9.1'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'redis.clients:jedis'
	runtimeOnly 'org.postgresql:postgresql'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.amqp:spring-rabbit-test'
	testImplementation 'org.springframework.amqp:spring-rabbit-test'
	testImplementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
	kapt 'org.mapstruct:mapstruct-processor:1.5.5.Final'
}

tasks.withType(KotlinCompile) {
	kotlinOptions {
		freeCompilerArgs += '-Xjsr305=strict'
		jvmTarget = '17'
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

import org.openapitools.generator.gradle.plugin.tasks.GenerateTask
tasks.register("kotlinExternalApi", GenerateTask) {
	kotlinExternalApi.generatorName = "kotlin-spring"
	kotlinExternalApi.inputSpec = "$rootDir/src/main/resources/openapi/external.yaml".toString()
	kotlinExternalApi.outputDir = "$buildDir/generated-sources/openapi/external".toString()
	kotlinExternalApi.apiPackage = "hu.bme.aut.classifiedadvertisementsite.bidservice.api.external"
	kotlinExternalApi.modelPackage = "hu.bme.aut.classifiedadvertisementsite.bidservice.api.external.model"
	kotlinExternalApi.configOptions = [
			useTags: "true",
			interfaceOnly: "true",
			skipDefaultInterface: "true",
			exceptionHandler: "false"
	]

	doLast {
		// exclude org.* to prevent unused SpringDocConfiguration class collision
		delete ("$buildDir/generated-sources/openapi/external/src/main/kotlin/org")
	}
}

tasks.register("kotlinInternalApi", GenerateTask) {
	kotlinInternalApi.generatorName = "kotlin-spring"
	kotlinInternalApi.inputSpec = "$rootDir/src/main/resources/openapi/internal.yaml".toString()
	kotlinInternalApi.outputDir = "$buildDir/generated-sources/openapi/internal".toString()
	kotlinInternalApi.apiPackage = "hu.bme.aut.classifiedadvertisementsite.bidservice.api.internal"
	kotlinInternalApi.modelPackage = "hu.bme.aut.classifiedadvertisementsite.bidservice.api.internal.model"
	kotlinInternalApi.configOptions = [
			useTags: "true",
			interfaceOnly: "true",
			skipDefaultInterface: "true",
			exceptionHandler: "false"
	]

	doLast {
			// exclude org.*to prevent unused SpringDocConfiguration class collision
		delete ("$buildDir/generated-sources/openapi/internal/src/main/kotlin/org")
	}
}

tasks.register("userServiceInternalApiClient", GenerateTask) {
	userServiceInternalApiClient.generatorName = "java"
	userServiceInternalApiClient.library = "resttemplate"
	userServiceInternalApiClient.inputSpec = "$rootDir/../userservice/src/main/resources/openapi/internal.yaml".toString()
	userServiceInternalApiClient.outputDir = "$buildDir/generated-sources/openapi/client".toString()
	userServiceInternalApiClient.apiPackage = "hu.bme.aut.classifiedadvertisementsite.bidservice.client.java.api"
	userServiceInternalApiClient.modelPackage = "hu.bme.aut.classifiedadvertisementsite.bidservice.client.java.api.model"
	userServiceInternalApiClient.generateApiDocumentation = false
	userServiceInternalApiClient.generateApiTests = false
	userServiceInternalApiClient.generateModelDocumentation = false
	userServiceInternalApiClient.generateModelTests = false
}

sourceSets {
	main {
		kotlin {
			srcDir("$buildDir/generated-sources/openapi/external/src/main/kotlin")
			srcDir("$buildDir/generated-sources/openapi/internal/src/main/kotlin")
		}
	}
}
java.sourceSets["main"].java {
	srcDir("$buildDir/generated-sources/openapi/client/src/main/java")
}

compileKotlin.dependsOn('kotlinExternalApi', 'kotlinInternalApi', 'userServiceInternalApiClient')

jar {
	enabled = false
}
